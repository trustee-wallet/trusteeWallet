// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        firebaseMessagingVersion = "21.1.0"
        buildToolsVersion = "30.0.3"
        minSdkVersion = 21
        compileSdkVersion = 31
        targetSdkVersion = 31
		kotlinVersion = "1.6.0"
    }
    repositories {
        google()
        maven {
            url 'https://maven.fabric.io/public'
        }
        mavenCentral()
    }
    dependencies {
        classpath('com.android.tools.build:gradle:4.1.2')
        classpath 'com.google.gms:google-services:4.2.0'
        classpath 'io.fabric.tools:gradle:1.25.4'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
        classpath 'com.google.firebase:perf-plugin:1.3.5'
    }
}

allprojects {
    repositories {
        exclusiveContent {
            // https://github.com/facebook/react-native/issues/35210
            // We get React Native's Android binaries exclusively through npm,
            // from a local Maven repo inside node_modules/react-native/.
            // (The use of exclusiveContent prevents looking elsewhere like Maven Central
            // and potentially getting a wrong version.)
            filter {
                includeGroup "com.facebook.react"
            }
            forRepository {
                maven {
                    url "$rootDir/../node_modules/react-native/android"
                }
            }
        }
        mavenLocal()
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url("$rootDir/../node_modules/react-native/android")
        }
        maven {
            // Android JSC is installed from npm
            url("$rootDir/../node_modules/jsc-android/dist")
        }

        google()
	jcenter() {
    		content {
        		includeModule("com.github.florent37", "singledateandtimepicker")	// Required by fioprotocol_fiosdk
        		includeModule("com.jraska", "console")					// Required by fioprotocol_fiosdk
        		includeModule("com.eightbitlab", "blurview")				// Required by react-native-community_blur
                includeGroup("com.google.android.exoplayer")
    		}
	}
        maven { url 'https://jitpack.io' }
        mavenCentral()
    }
}

subprojects {
    afterEvaluate {project ->
        if (project.hasProperty("android")) {
            android {
                compileSdkVersion rootProject.ext.compileSdkVersion
                buildToolsVersion rootProject.ext.buildToolsVersion
            }
        }
    }
}
